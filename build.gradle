/*
 * Copyright (C) 2023 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
    ext {
        kotlinVersion = '1.9.20'
        gradlePluginVersion = '8.3.0'
        lintVersion = '31.5.1'
    }

    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$gradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
}

java {
    withSourcesJar()
    withJavadocJar()
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.android.security.lint'
            artifactId = 'lint'
            version = '1.1'

            from components.java

            pom {
                name = 'Android Security Lints'
                description = 'Custom security-focused experimental lint checks for Android development'
                url = 'https://github.com/google/android-security-lints'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'com.android.security.lint'
                        name = 'Android Security Lint Contributors'
                        url = 'https://github.com/google/android-security-lints'
                        organization = 'Google - Android 3P Vulnerability Research'
                        organizationUrl = 'https://www.google.com'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/google/android-security-lints.git'
                    developerConnection = 'scm:git:git@github.com:google/android-security-lints.git'
                    url = 'https://github.com/google/android-security-lints'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('androidsecuritylints_m2repo')
        }
    }
}
